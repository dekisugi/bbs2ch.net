#============================================================================================================
#
#	拡張機能 - 1専用スレッド
#	0ch_orz_thread.pl
#	ぜろちゃんねるプラス0.7.4以降専用
#	---------------------------------------------------------------------------
#	# errmsg.cgi用
#	1062<>&gt;&gt;1専用スレッド<>>>1さん以外書き込みできないスレッドです^^;
#	---------------------------------------------------------------------------
#	2017.09.15 start
#
#============================================================================================================
package ZPL_orz_thread;

use strict;

#------------------------------------------------------------------------------------------------------------
#	拡張機能名称取得
#------------------------------------------------------------------------------------------------------------
sub getName
{
	return '1専用スレッド';
}

#------------------------------------------------------------------------------------------------------------
#	拡張機能説明取得
#------------------------------------------------------------------------------------------------------------
sub getExplanation
{
	return 'メール欄に!orzで1しか書き込めなくなります。!syaki-nで随時解除できます。';
}

#------------------------------------------------------------------------------------------------------------
#	拡張機能タイプ取得
#------------------------------------------------------------------------------------------------------------
sub getType
{
	return (1|2);
}

#------------------------------------------------------------------------------------------------------------
#	設定リスト取得 (0ch+ Only)
#------------------------------------------------------------------------------------------------------------
sub getConfig
{
	return {};
}

#------------------------------------------------------------------------------------------------------------
#	拡張機能実行インタフェイス
#------------------------------------------------------------------------------------------------------------
sub execute
{
	my $this = shift;
	my ($Sys, $Form, $type) = @_;
	
	# 0ch本家では実行しない
	return 0 if (!$this->{'is0ch+'});
	
	my $CGI = $Sys->Get('MainCGI');
	
	# baggins.pl BILBO : subjectの管理が主、属性情報の管理も兼ねる
	my $Threads = $CGI->{'THREADS'} || $Sys->Get('_THREAD_');
	
	# 対象スレッドの番号
	my $threadid = $Sys->Get('KEY');
	# 対象板ディレクトリ名
	my $bbs = $Sys->Get('BBS');
	
	# 属性情報を扱うには一度でも読み込む必要がある
	$Threads->LoadAttr($Sys);
	
	# 属性を取得、属性情報がなければ空文字列
	my $orz = $Threads->GetAttr($threadid, 'ZPL_orz_thread');
	
	# メール欄を取得
	my $mail = $Form->Get('mail');
	
	# orz属性が設定されている or これから設定しようとしている 訳でないなら実行しない
	return 0 unless ($orz or (index($mail, '!orz') ne -1));
	
	# スレ主フラグ
	my $isowner = 0;
	
	# トリップ(変換後)を取得
	my $trip = $Form->Get('TRIPKEY');
	
	# キャップ管理モジュールを準備
	my $Sec = SECURITY->new;
	$Sec->Init($Sys);
	$Sec->SetGroupInfo($bbs);
	
	# システム共通権限キャップ ＝ スレ主扱い
	if ($Sec->IsAuthority($Sys->Get('CAPID', ''), 0, '*')) {
		$isowner = 'cap';
	# モードがスレ立て and トリップを付けている ＝ スレ主扱い
	} elsif ($Sys->Equal('MODE', 1) and $trip) {
		$isowner = 'trip';
	# モードがレス書き込み
	} elsif ($Sys->Equal('MODE', 2)) {
		# galadriel.pl
		my $Conv = $CGI->{'CONV'};
		
		# DATのPATH
		my $path = $Conv->MakePath($Sys->Get('BBSPATH')."/$bbs/dat/$threadid.dat");
		
		# DATから>>1の名前欄を取ってくる
		my $ownername;
		if (open my $fh, '<', $path) {
			flock $fh, 1;
			seek $fh, 0, 0;
			($ownername) = split /<>/, <$fh>;
			close $fh;
		# DATの読み込みに失敗した
		} else {
			warn "ZPL_orz_thread: can't read `$path'";
			# PrintBBSError($Sys, 902);
			return 0;
		}
		
		# >>1がトリップを付けていたらそれを切り取る
		if ($ownername =~ m#</b>◆([./0-9A-Za-z]+) <b>#) {
			# >>1のトリップと書き込もうとしている人のトリップが一致 ＝ スレ主扱い
			$isowner = 'trip' if ($1 eq $trip);
		}
	}
	
	# !orz属性がある場合の処理
	if ($orz) {
		if ($isowner eq 'cap') {
			# キャップはスレ主に拘らず書き込める
		} elsif (($isowner eq 'trip') and ($orz eq 'trip')) {
			# トリップで認証した人はスレ主もそうであれば書き込める
		} else {
			# そうではない人が書き込もうとしたらエラー
			PrintBBSError($Sys, 1062);
		}
	}
	
	# スレ主ならメール欄のコマンドを解釈する
	if ($isowner) {
		if (index($mail, '!orz') ne -1) {
			# メール欄に!orzがあったら>>1専用属性を設定
			$Threads->SetAttr($threadid, 'ZPL_orz_thread', $isowner);
			# 属性を変更したら保存
			$Threads->SaveAttr($Sys);
		} elsif (index($mail, '!syaki-n') ne -1) {
			# メール欄に!syaki-nがあったら>>1専用属性を削除
			$Threads->DeleteAttr($threadid, 'ZPL_orz_thread');
			# 属性を変更したら保存
			$Threads->SaveAttr($Sys);
		}
	}
	
	return 0;
}

#------------------------------------------------------------------------------------------------------------
#	なんちゃってbbs.cgiエラーページ表示
#------------------------------------------------------------------------------------------------------------
sub PrintBBSError
{
	my ($Sys, $err) = @_;
	
	require './module/orald.pl';
	
	my $CGI = $Sys->Get('MainCGI');
	my $Page = $CGI->{'PAGE'};
	
	my $Error = ORALD->new;
	$Error->Load($Sys);
	$Error->Print($CGI, $Page, $err, $Sys->Get('AGENT'));
	
	$Page->Flush('', 0, 0);
	
	exit($err);
}



#------------------------------------------------------------------------------------------------------------
#	コンストラクタ
#------------------------------------------------------------------------------------------------------------
sub new
{
	my $class = shift;
	my ($Config) = @_;
	
	my $this = {};
	bless $this, $class;
	
	if (defined $Config) {
		$this->{'PLUGINCONF'} = $Config;
		$this->{'is0ch+'} = 1;
	}
	else {
		$this->{'CONFIG'} = $class->getConfig();
		$this->{'is0ch+'} = 0;
	}
	
	return $this;
}

#------------------------------------------------------------------------------------------------------------
#	設定値取得 (0ch+ Only)
#------------------------------------------------------------------------------------------------------------
sub GetConf
{
	my $this = shift;
	my ($key) = @_;
	if ($this->{'is0ch+'}) {
		return $this->{'PLUGINCONF'}->GetConfig($key);
	}
	elsif (defined $this->{'CONFIG'}->{$key}) {
		return $this->{'CONFIG'}->{$key}->{'default'};
	}
}

#------------------------------------------------------------------------------------------------------------
#	設定値設定 (0ch+ Only)
#------------------------------------------------------------------------------------------------------------
sub SetConf
{
	my $this = shift;
	my ($key, $val) = @_;
	if ($this->{'is0ch+'}) {
		$this->{'PLUGINCONF'}->SetConfig($key, $val);
	}
	elsif (defined $this->{'CONFIG'}->{$key}) {
		$this->{'CONFIG'}->{$key}->{'default'} = $val;
	}
	else {
		$this->{'CONFIG'}->{$key} = { 'default' => $val };
	}
}

#============================================================================================================
#	Module END
#============================================================================================================
1;
